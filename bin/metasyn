#!perl

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;

our %SPEC;

$SPEC{app} = {
    v => 1.1,
    summary => 'Alternative front-end to Acme::MetaSyntactic',
    args => {
        action => {
            schema => ['str*', in=>[qw/list-themes list-names/]],
            default => 'list-names',
            cmdline_aliases => {
                l => { summary => 'List installed themes', is_flag => 1, code => sub { $_[0]{action} = 'list-themes' } },
            },
        },
        theme => {
            schema => 'str*',
            pos => 0,
            completion => sub {
                require Complete::Acme::MetaSyntactic;
                Complete::Acme::MetaSyntactic::complete_meta_themes_and_categories(@_);
            },
        },
        shuffle => {
            schema => ['bool*', is=>1],
        },
        categories => {
            schema => ['bool*', is=>1],
            cmdline_aliases => {c=>{}},
        },
    },
};
sub app {
    no strict 'refs';
    require Acme::MetaSyntactic;

    my %args = @_;

    my $action = $args{action};

    return [200, "OK", [Acme::MetaSyntactic->new->themes]]
        if $action eq 'list-themes';

    my $theme = $args{theme};
    return [400, "Please specify theme"] unless $theme;
    my $cat = $theme =~ s{/(.+)\z}{} ? $1 : undef;

    my $pkg = "Acme::MetaSyntactic::$theme";
    (my $pkg_pm = "$pkg.pm") =~ s!::!/!g;
    return [500, "Can't load $pkg: $@"] unless (eval { require $pkg_pm; 1 });

    if ($args{categories}) {
        my @res;
        eval { @res = $pkg->categories };
        #warn if $@;
        return [200, "OK", \@res];
    }
    #my $meta = Acme::MetaSyntactic->new($theme);
    my @names;
    if (defined $cat) {
        @names = @{ ${"$pkg\::MultiList"}{$cat} // [] };
    } else {
        @names = @{"$pkg\::List"};
    }
    if ($args{shuffle}) {
        require List::Util;
        @names = List::Util::shuffle(@names);
    }
    return [200, "OK", \@names];
}

require Perinci::CmdLine::Any;
Perinci::CmdLine::Any->new(url => '/main/app')->run;

1;
#ABSTRACT:
#PODNAME:

=head1 SYNOPSIS

List all installed themes:

 % metasyn -l

List all names from a theme:

 % metasyn foo
 % metasyn christmas/elf
 % metasyn foo --shuffle

List all categories from a theme:

 % metasyn christmas --categories ;# or -c


=head1 DESCRIPTION

This script is an alternative front-end to L<Acme::MetaSyntactic>. Compare to
the official CLI L<meta>, C<meta>, this CLI is more oriented towards listing
names instead of giving you one or several random names.


=head1 SEE ALSO

L<meta> from L<Acme::MetaSyntactic>
